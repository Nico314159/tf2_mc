class Projectile {
    new advancement(take_hit) {
    	"criteria": {
		    "requirement": {
	    		"trigger": "minecraft:entity_hurt_player",
			    "conditions": {
    	  			"damage": {
	    				"type": {
				 		    "direct_entity": {
					        	"type": "minecraft:arrow"
    					    }
		        		}
				    }
    			}
	    	}
    	},
		"rewards": {"function": "tf2:projectile/count_hits"}
	}

    // Gets the position of the executing entity and puts it into tf2.[x,y,z]
    function get_pos() {
        lookup::pos = @s::Pos;
        Hardcode.repeatLists(
            ["[AXIS]", "[INDEX]"],
            () => {
                tf2.[AXIS]:@s = lookup::pos[[INDEX]] * 1000;
            },
            [
                ["x", "y", "z"],
                ["0", "1", "2"]
            ]
        );
    }

    function spawn() {
        // testing code
        tf2.team:$temp = tf2.team:@s;
        tf2.player.id:$temp = tf2.player.id:@s;
        tf2.batch_number:$local = tf2.batch_number:@s;
        Math.facing_vector();
        summon::projectile = {X: 0.0, Y: 0.0, Z: 0.0, yaw: 0.0, pitch: 0.0, spawnX: 0.0, spawnY: 0.0, spawnZ: 0.0};

        // Convert HU/sec -> blocks/sec
        summon::projectile.speed = 0.01905 * (double) lookup::item.components.minecraft:custom_data.projectile.speed;
        summon::projectile.speed_range = lookup::item.components.minecraft:custom_data.projectile.speed_range;
        if (data storage tf2:summon projectile.speed_range) {
            summon::projectile.speed = 0.01905 * (double) Math.macro_random() 
                                       with summon::projectile.speed_range;
        }
        // Convert blocks/sec -> blocks/tick
        summon::projectile.speed = EVAL(1  / 20_000) * (double) summon::projectile.speed * 1000;

        summon::projectile.deviation = lookup::item.components.minecraft:custom_data.projectile.deviation;
        if (data storage tf2:summon projectile.deviation) {
            Projectile.deviation();
        }
        Hardcode.repeatList(
            "[AXIS]",
            () => { 
                $mot.mot_[AXIS] = summon::projectile.speed * 1000;
                $mot.mot_[AXIS] *= $output_vector_[AXIS];
                $mot.mot_[AXIS] /= 1000;
            },
            ["x", "y", "z"]
        );

        summon::projectile.yaw = 0.1 * (double) $input_yaw;
        summon::projectile.pitch = 0.1 * (double) $input_pitch;
        summon::projectile.weapon = lookup::item;

        $relative_to_player = lookup::item.components.minecraft:custom_data.projectile.relative_to_player;
        if ($relative_to_player) { Projectile.compensate_velocity(); }

        summon::projectile.spawnX = lookup::item.components.minecraft:custom_data.projectile.spawn[0];
        summon::projectile.spawnY = lookup::item.components.minecraft:custom_data.projectile.spawn[1];
        summon::projectile.spawnZ = lookup::item.components.minecraft:custom_data.projectile.spawn[2];
        summon::projectile.weapon_name = lookup::item.components.minecraft:custom_name;
        summon::projectile.CMD = 100 * lookup::item.components.minecraft:custom_model_data;
        summon::projectile.owner = @s::UUID;
        Hardcode.repeatList("|property|", () => {
            $|property| = lookup::item.components.minecraft:custom_data.projectile.|property|;
        }, Projectile.SPECIAL_PROPERTIES);
        summon::projectile.no_gravity ?= execute if score $gravity tf2.var matches 0;

        execute at @s anchored eyes 
                run this.spawn_internal()
                with summon::projectile;

        execute as @n[type=item_display,tag=tf2.new,tag=tf2.projectile] run {
            tf2.age:@s = 0;
            tf2.team:@s = tf2.team:$temp;
            tf2.player.id:@s = tf2.player.id:$temp;
            tf2.batch_number:@s = tf2.batch_number:$local;

            tf2.model_number:@s = summon::projectile.CMD;
            @s::billboard = lookup::item.components.minecraft:custom_data.projectile.billboard;
            @s::item.components.minecraft:custom_data.attributes = lookup::item.components.minecraft:custom_data.attributes;
            @s::item.components.minecraft:custom_data.projectile = lookup::item.components.minecraft:custom_data.projectile;
            
            tf2.lifetime:@s = Projectile.calc_lifetime();

            @s::item.components.minecraft:custom_name = summon::projectile.weapon_name; 

            Hardcode.repeatList("|property|", () => {
                if ($|property|) {
                    tag @s add tf2.|property|;
                }
            }, Projectile.SPECIAL_PROPERTIES);

            this.get_pos();
            Hardcode.repeatLists(
                ["[AXIS]", "[INDEX]"],
                () => {
                    tf2.mot_[AXIS]:@s = $mot.mot_[AXIS];
                    tf2.hitbox_[AXIS]:@s = lookup::item.components.minecraft:custom_data.projectile.hitbox * EVAL(1000 * 0.01905);
                },
                [
                    ["x", "y", "z"],
                    ["0", "1", "2"]
                ]
            );

            if (data storage tf2:lookup item.components.minecraft:custom_data.projectile.forces.torque) {
                Math.random_quaternion();
                Hardcode.repeatList(
                    "[PARAM]",
                    () => { tf2.quaternion_[PARAM]:@s = $output_quaternion_[PARAM]; },
                    ["r", "i", "j", "k"]
                );

                Math.random_vector();
                Hardcode.repeatList(
                    "[AXIS]",
                    () => {
                        tf2.ang_vel_[AXIS]:@s = $output_vector_[AXIS];
                        tf2.ang_vel_[AXIS]:@s *= GRENADE_ANGULAR_SPEED;
                        tf2.ang_vel_[AXIS]:@s /= 1000;
                    },
                    ["x", "y", "z"]
                );
            }

            tag @s remove tf2.new;
        }
    }
    function spawn_internal() {
        $ summon item_display ^$(spawnX) ^$(spawnY) ^$(spawnZ) {
            Rotation: [$(yaw)f, $(pitch)f],
            Tags: ["tf2.projectile", "tf2.new"],
            item: {id: "minecraft:glowstone_dust", components:{
                "minecraft:custom_model_data": $(CMD), 
                "minecraft:custom_data": {}
            }},
            start_interpolation: -1,
            item_display: "ground",
            transformation: {
                right_rotation: [0.0f, 0.0f, 0.0f, 1.0f],
                scale: [1.0f, 1.0f, 1.0f],
                left_rotation: [0.0f, 0.0f, 0.0f, 1.0f],
                translation: [0.0f, 0.0f, 0.0f]
            },
            interpolation_duration: 1,
            teleport_duration: 1
        };
    }

    function tick() {
        if (entity @s[tag=tf2.has_trail]) {
            execute at @s run Projectile.trail();
        }
        if (entity @s[tag=tf2.animated]) {
            Projectile.animate();
        }


        if (entity @s[tag=!tf2.stuck]) {
            Motion.move(); 
        }
        if (tf2.collision:@s) {
            this.handle_block_collision();
        }
        if (entity @s[tag=!tf2.stuck]) {
            this.handle_rotation();
        }
        if (entity @s[tag=!tf2.stuck] && 
            data entity @s item.components.minecraft:custom_data.projectile.forces) {
            this.apply_forces();
        }

        $width = tf2.hitbox_x:@s;
        $margin = 500;
        $width += $margin; // Optional
        $width > 1000;
        vars::offset = 0.0005 * (float) $width;
        $width -= 1000;
        vars::volume = 0.001 * (float) $width;
        this.check_entity_collision() with vars::;
        if ($hit_entity == 0) { tag @s add tf2.has_escaped_player; }
        if ($hit_entity == 1 && entity @s[tag=tf2.has_escaped_player]) {
            this.handle_entity_collision();
        }

        tf2.age:@s++;
        if (tf2.age:@s > tf2.lifetime:@s) {
            this.end_of_life();
        }
       
        kill @s[tag=tf2.delete];

        execute as @e[type=#tf2:player_like,tag=tf2.batch] run Projectile.remove_tags();
    }
    function count_hits() {
        tf2.hits_taken:@s++;
        advancement revoke @s only tf2:projectile/take_hit;
    }
    function trail() {
        Hardcode.repeat("|z|", () => {
            particle minecraft:smoke ^ ^-0.2 ^Hardcode.calc(|z| / -3) 0 0 0 0 10;
        }, start=1, stop=4);
    }
    function animate() {
        tf2.model_number:@s++;
        item modify entity @s contents { 
            "function": "minecraft:set_custom_model_data",
            "value": {"type": "minecraft:score", "target": "this", "score": "tf2.model_number"}
        };
    }
    function calc_lifetime() {
        if (data storage tf2:summon projectile.weapon.components.minecraft:custom_data.projectile.lifetime) {
            return run summon::projectile.weapon.components.minecraft:custom_data.projectile.lifetime;
        }
        
        if (data storage tf2:summon projectile.weapon.components.minecraft:custom_data.projectile.lifetime_range) {
            return run Math.macro_random() 
                       with summon::projectile.weapon.components.minecraft:custom_data.projectile.lifetime_range;
        }

        return Projectile.DEFAULT_LIFETIME;
    }
    function init_arrow() {
        // @needs_macro
        $ summon arrow ^$(spawnX) ^$(spawnY) ^$(spawnZ) {
            Rotation: [$(yaw)f, $(pitch)f],
            Owner: $(owner),
            damage: 0.001,
            pickup: false,
            NoGravity: $(no_gravity),
            Silent: true,
            PierceLevel: 127b,
            PersistenceRequired: true,
            ShotFromCrossbow: true,
            Tags: ["tf2.projectile", "tf2.new"],
            Motion: [$(X)d, $(Y)d, $(Z)d],
            active_effects: [{
                id: "minecraft:invisibility",
                amplifier: false,
                show_particles: false,
                duration: -1
            }],
            Passengers: [{
                id: "minecraft:item_display",
                item: {id: "minecraft:glowstone_dust", components:{
                    "minecraft:custom_model_data": $(CMD), 
                    "minecraft:custom_data": {Motion:[$(X)d, $(Y)d, $(Z)d]}
                }},
                start_interpolation: -1,
                item_display: "ground",
                Tags: ["tf2.new", "tf2.projectile"]
            }]
        };
    }
    function deviation() {
        function retina:math/gimbal_to_matrix;

        retina.__variable__:$horizontal_count = retina.__variable__:$vertical_count = 10;
        retina.__variable__:$spread_least = summon::projectile.deviation.min;
        retina.__variable__:$spread_most = summon::projectile.deviation.max;

        function retina:math/sway_heave_surge/find;
        function retina:math/sway_heave_surge/apply;
    }
    function copy_xz_motion() {
        @s::Motion[0] = vars::Motion[0];
        @s::Motion[2] = vars::Motion[2];
    }
    function compensate_velocity() {
        $velocity = summon::projectile.X * 100;
        $velocity += tf2.now_x:@s;
        $velocity -= tf2.last_x:@s;
        summon::projectile.X = 0.01 * (double) $velocity;

        $velocity = summon::projectile.Z * 100;
        $velocity += tf2.now_z:@s;
        $velocity -= tf2.last_z:@s;
        summon::projectile.Z = 0.01 * (double) $velocity;
    }
    function convection() {
        $velocity = @s::Motion[1] * 1000;
        $accel = execute on passengers run @s::item.components.minecraft:custom_data.projectile.convection.factor * 1000;
        $velocity += $accel;
        @s::Motion[1] = 0.001 * (double) $velocity;
    }
    function apply_forces() {
        forces::{} += @s::item.components.minecraft:custom_data.projectile.forces;

        $gravity = forces::gravity * 1000;
        tf2.mot_y:@s -= $gravity;


        $friction = 1000;
        if (data storage tf2:forces drag) {
            $friction = forces::drag * 1000;
        }
        if (tf2.collision:@s && data storage tf2:forces collision_friction) {
            $mu = forces::collision_friction * 1000;
            $friction *= $mu;
            $friction /= 1000;
        }
    
        Hardcode.repeatList(
            "[AXIS]",
            () => {
                tf2.mot_[AXIS]:@s *= $friction;
                tf2.mot_[AXIS]:@s /= 1000;
            },
            ["x", "y", "z"]
        );

        tf2.mot_y:@s[scores={tf2.collision_y=-1}] = 0;
    }

    function handle_block_collision() {
        $mode = @s::item.components.minecraft:custom_data.projectile.block_collision.mode;
        
        switch ($mode) {
            case Collision.CONTINUE:
            break;

            case Collision.IMPACT:
            this.impact();

            case Collision.BOUNCE:
            $impact_if_first = @s::item.components.minecraft:custom_data.projectile.block_collision.impact_if_first;
            if ($impact_if_first && entity @s[tag=!tf2.has_hit_wall, tag=!tf2.has_hit_entity]) {
                this.impact();
            } 
            else {
                this.block_bounce();
            }

            case Collision.STICK:
            tf2.halt:@s = true;
            tag @s add tf2.stuck;
        }
    }
    function block_bounce() {
        $elasticity = @s::item.components.minecraft:custom_data.projectile.block_collision.elasticity * 1000;
        // Reduce speed when it hits a wall
        if (tf2.collision_x:@s != 0 || tf2.collision_z:@s != 0) {
            Hardcode.repeatList(
                "[AXIS]",
                () => {
                    tf2.mot_[AXIS]:@s *= $elasticity;
                    tf2.mot_[AXIS]:@s /= 1000;
                },
                ["x", "y", "z"]
            );
        }

        // Flag for wall hits
        tag @s[scores={tf2.collision=1}] add tf2.has_hit_wall;

        // Apply bounces
        Hardcode.repeatList(
            "[AXIS]",
            () => { if (tf2.collision_[AXIS]:@s != 0) tf2.mot_[AXIS]:@s *= -1; },
            ["x", "y", "z"]
        );

        tag @s add tf2.has_hit_wall;
    }

    function check_entity_collision() {
        // Narrow search space to only the current game
        tf2.batch_number:$local = tf2.batch_number:@s;
        tag @e remove tf2.search;
        execute as @e[type=#tf2:player_like]
                if score @s tf2.batch_number
                   = $local tf2.batch_number
                unless entity @s[gamemode=spectator]
                run tag @s add tf2.batch;
        tag @e[tag=tf2.batch] add tf2.search;

        // Passes through teammates during first 0.25 seconds
        tf2.team:$temp = tf2.team:@s;
        execute as @e[type=#tf2:player_like,tag=tf2.search]
                unless score @s tf2.team = $temp tf2.team
                run tag @s add tf2.opponent;
        if (tf2.age:@s < 5) {
            tag @e[type=#tf2:player_like,tag=tf2.search,tag=!tf2.opponent] remove tf2.search;
        }

        // Detect hitting players and objects
        tag @e remove tf2.hit_by_projectile;
        $ execute positioned ~-$(offset) ~-$(offset) ~-$(offset) 
                  as @n[type=#tf2:player_like,tag=tf2.search,dx=$(volume),dy=$(volume),dz=$(volume)] 
                  run tag @s add tf2.hit_by_projectile;

        $hit_entity ?= execute if entity @e[tag=tf2.hit_by_projectile];
    }
    function handle_entity_collision() {
        $mode = @s::item.components.minecraft:custom_data.projectile.entity_collision.mode;
        
        switch ($mode) {
            case Collision.CONTINUE:
            this.impact();
            tag @s remove tf2.delete;

            case Collision.IMPACT:
            this.impact();

            case Collision.BOUNCE:
            $impact_if_first = @s::item.components.minecraft:custom_data.projectile.entity_collision.impact_if_first;
            if ($impact_if_first && entity @s[tag=!tf2.has_hit_wall, tag=!tf2.has_hit_entity]) {
                this.impact();
            } 
            else {
                this.entity_bounce();
            }
        }
    }
    function entity_bounce() {
        $elasticity = @s::item.components.minecraft:custom_data.projectile.entity_collision.elasticity * -1000;
        Hardcode.repeatList(
            "[AXIS]",
            () => {
                tf2.mot_[AXIS]:@s *= $elasticity;
                tf2.mot_[AXIS]:@s /= 1000;
            },
            ["x", "z"]
        );
        tag @s add tf2.has_hit_object;
    }

    function impact() {
        lookup::item = @s::item;
        $_damage_ = lookup::item.components.minecraft:custom_data.attributes.damage.base;
        $_selfDamage_ = lookup::item.components.minecraft:custom_data.projectile.explosion.selfDamage;
        $_maxRamp_ = lookup::item.components.minecraft:custom_data.attributes.damage.maxRamp * 100;
        if (!data storage tf2:lookup item.components.minecraft:custom_data.attributes.damage.maxRamp) $_maxRamp_ = 150;
        $_temporalFalloff_ = lookup::item.components.minecraft:custom_data.attributes.damage.temporalFalloff;
        $_rangeDependent_ = lookup::item.components.minecraft:custom_data.attributes.damage.uniform;
        $_rangeDependent_ ?= execute if score $_rangeDependent_ tf2.var matches 0;

        vars::weapon_name = lookup::item.components.minecraft:custom_name;

        tag @s add tf2.delete;
        if (entity @s[tag=!tf2.explosion]) return run {
            Projectile.simple_damage();
        }

        vars::radius = 0.01905 * (double) lookup::item.components.minecraft:custom_data.projectile.explosion.radius;
        Projectile.explode() with vars::;
    }

    function check_collision() {
        tf2.batch_number:$local = tf2.batch_number:@s;
        execute as @e[type=#tf2:player_like]
                if score @s tf2.batch_number
                   = $local tf2.batch_number
                unless entity @s[gamemode=spectator]
                run tag @s add tf2.batch; 
        tag @e[type=#tf2:player_like,tag=tf2.batch] add tf2.search;

        // Passes through teammates during first 0.25 seconds
        tf2.team:$temp = tf2.team:@s;
        execute as @e[type=#tf2:player_like,tag=tf2.search]
                unless score @s tf2.team = $temp tf2.team
                run tag @s add tf2.opponent;
        if (tf2.age:@s < 5) {
            tag @e[type=#tf2:player_like,tag=tf2.search,tag=!tf2.opponent] remove tf2.search;
        }

        $collided = false;
        execute on vehicle if data entity @s {inGround: true} run {
            $collided++;
        }

        execute on vehicle at @s as @p[scores={tf2.hits_taken=1..}, tag=tf2.search] run tag @s add tf2.hit;
        execute as @a[tag=tf2.hit] run tf2.hits_taken:@s--;
        if (entity @a[tag=tf2.hit]) {
            $collided += 2;
        }

        if (!$collided) return 0;

        lookup::item = @s::item;
        $_damage_ = lookup::item.components.minecraft:custom_data.attributes.damage.base;
        $_selfDamage_ = lookup::item.components.minecraft:custom_data.projectile.explosion.selfDamage;
        $_maxRamp_ = lookup::item.components.minecraft:custom_data.attributes.damage.maxRamp * 100;
        if (!data storage tf2:lookup item.components.minecraft:custom_data.attributes.damage.maxRamp) $_maxRamp_ = 150;
        $_temporalFalloff_ = lookup::item.components.minecraft:custom_data.attributes.damage.temporalFalloff;
        $_rangeDependent_ = lookup::item.components.minecraft:custom_data.attributes.damage.uniform;
        $_rangeDependent_ ?= execute if score $_rangeDependent_ tf2.var matches 0;

        tag @s[tag=!tf2.ignore_env] add tf2.cleanup;
        vars::weapon_name = lookup::item.components.minecraft:custom_name;

        if (entity @s[tag=!tf2.explosion]) return run {
            this.simple_damage();
        }

        vars::radius = 0.01905 * (double) lookup::item.components.minecraft:custom_data.projectile.explosion.radius;
        this.explode() with vars::;
    }
    function simple_damage() {
        if (!entity @a[tag=tf2.hit_by_projectile,tag=tf2.opponent,scores={tf2.health=1..}]) {return fail;}

        tf2.player.id:$temp = tf2.player.id:@s;
        execute as @a if score @s tf2.player.id = $temp tf2.player.id run tag @s add tf2.origin;
        
        $_finalDamage_ = $_damage_;
        if (!$_temporalFalloff_) expand {
            this.pythagorean_3D();
            Weapons.calculate_falloff();
        }
        if ($_temporalFalloff_) {
            this.temporal_falloff();
        }
        $_finalDamage_ *= $_multiplier_;
        // printf ("mult = &<$_multiplier_>");
        $_finalDamage_ += 99;
        $_finalDamage_ /= 100;

        lookup::damages << {};
        lookup::damages[-1].cause = tf2.player.id:@s;
        lookup::damages[-1].target = tf2.player.id:@p[tag=tf2.hit_by_projectile];
        lookup::damages[-1].amount = $_finalDamage_;
        lookup::damages[-1].kill_verb = "killed";
        lookup::damages[-1].weapon = vars::weapon_name;

        vars::callbacks = @s::item.components.minecraft:custom_data.attributes.on_hit;
        vars::callback = vars::callbacks[0];
        if (data storage tf2:vars callbacks[]) {
            this.callbacks() with vars::;
        }

    }
    function callbacks() {
        // @needs_macro
        vars::callback = vars::callbacks[0];
        $ $(callback);
        vars::callbacks[0].del();
        if (data storage tf2:vars callbacks[]) {
            this.callbacks() with vars::;
        }
    }
    function pythagorean_3D() {
        execute as @e[tag=tf2.origin] at @s summon marker run {
            $X0 = @s::Pos[0] * 100;
            $Y0 = @s::Pos[1] * 100;
            $Z0 = @s::Pos[2] * 100;
            kill;
        }
        execute at @s summon marker run {
            $Xf = @s::Pos[0] * 100;
            $Yf = @s::Pos[1] * 100;
            $Zf = @s::Pos[2] * 100;
            kill;
        }
        $deltaX = $Xf; $deltaX -= $X0; 
        $deltaY = $Yf; $deltaY -= $Y0; 
        $deltaZ = $Zf; $deltaZ -= $Z0; 
        summon::matrix = [
            0f, 0f, 0f, 0f,
            0f, 0f, 0f, 0f,
            0f, 0f, 0f, 0f,
            0f, 0f, 0f, 1f
        ];
        summon::matrix[0] = (float) $deltaX;
        summon::matrix[4] = (float) $deltaY;
        summon::matrix[8] = (float) $deltaZ;
        execute summon item_display run {
            @s::transformation = tf2:summon::matrix;
            retina:output::Distance = 0.01 * (double) @s::transformation.scale[0];
            kill;
        }
    }
    function temporal_falloff() {
        $_multiplier_ = tf2.age:@s; 
        $_multiplier_ *= 100;
        $_multiplier_ /= tf2.lifetime:@s;
        $_multiplier_ *= -1;
        $_multiplier_ += 100;
        $_multiplier_ > 50;
        $_multiplier_ < 100;
    }
    function explode() {
        particle minecraft:explosion ~ ~ ~;
        $playersHurt = $_totalDamage_ = 0;
        tf2.player.id:$temp = tf2.player.id:@s;
        execute as @a if score @s tf2.player.id = $temp tf2.player.id expand {
            tag @s add tf2.origin;
            tag @s add tf2.opponent;
        }

        $ execute at @s as @e[type=#tf2:player_like,tag=tf2.opponent,distance=..$(radius)] run this.wall_check();
        tag @a remove tf2.origin;
    }
    function wall_check() {
        tag @s add tf2.player;
        execute summon marker run {
            @s::NoAI = 1b;
            retina:input:: += {MaxRecursionDepth: 127b, VerticalCount: 1, HorizontalCount: 1, CenteredCount: 0, TargetEntities: true};
            rotate @s facing entity @e[tag=tf2.player,limit=1] eyes;
            function retina:traverse/setup;
            kill @s;
        }
        tag @s remove tf2.player;
        if (entity @s[tag=tf2.hit_by_projectile]) expand {
            retina:output::Distance = 0;
            this.splash_damage();
        }
        if (!entity @s[tag=tf2.hit_by_projectile] && data storage retina:output {Target: "ENTITY"} && Object.isEqual("retina:output", "TargetedEntity.UUID", "@e[type=#tf2:player_like,tag=tf2.check,limit=1]", "UUID")) {
            this.splash_damage();
        } 
    }
    function splash_damage() {
        $_finalDamage_ = this.calculate_splash();
        printf(" $_finalDamage_ = &<$_finalDamage_>");

        if ($_finalDamage_ > 0) {
            $playersHurt++;
        }

        lookup::damages << {};
        lookup::damages[-1].cause = tf2.player.id:@a[tag=tf2.origin,limit=1];
        lookup::damages[-1].target = tf2.player.id:@s;
        lookup::damages[-1].amount = $_finalDamage_;
        lookup::damages[-1].kill_verb = "blew up";
        lookup::damages[-1].weapon = vars::weapon_name;

        this.knockback();
    }
    function calculate_splash() {
        $_centerDist_ = retina:output::Distance * EVAL (1 / 0.01905);
        printf("$_centerDist_ = &<$_centerDist_>");
        $_splashEdge_ = vars::radius * EVAL (1 / 0.01905);
        printf("$_splashEdge_ = &<$_splashEdge_>");
        this.pythagorean_3D();
        printf("$_damage_ = &<$_damage_>");
        $_finalDamage_ = $_damage_;
        Weapons.calculate_falloff();
        $_finalDamage_ *= $_multiplier_;
        printf ("mult = &<$_multiplier_>");
        $_finalDamage_ += 99;
        $_finalDamage_ /= 100;
        printf("damage after multiplier = &<$_finalDamage_>");
        if (entity @s[tag=tf2.hit_by_projectile,tag=!tf2.origin]) {
            return run $_finalDamage_.get();
        }

        if (entity @s[tag=tf2.origin]) {
            $_finalDamage_ = $_selfDamage_;
        }
        printf("selfDamage = &<$_selfDamage_>");
        $_splash_multiplier_ = $_centerDist_;
        $_splash_multiplier_ *= 50;
        $_splash_multiplier_ /= $_splashEdge_;
        $_splash_multiplier_ *= -1;
        $_splash_multiplier_ += 100;
        printf("splash = &<$_splash_multiplier_>");
        $_finalDamage_ *= $_splash_multiplier_;
        $_finalDamage_ += 99;
        $_finalDamage_ /= 100;
        printf("damage after splash multiplier = &<$_finalDamage_>");
        if (!entity @s[tag=tf2.origin,scores={tf2.class=4..5}]) {
            return run $_finalDamage_.get();
        }

        // Less damage from your own explosions if nobody else is hurt
        // Soldier: 3/5 as much, Demo: 3/4 as much
        if ($playersHurt > 1) {
            return run $_finalDamage_.get();
        }
        $_finalDamage_ *= 3;
        $_temp_ = 9;
        $_temp_ -= tf2.class:@s;
        $_finalDamage_ /= $_temp_;
        printf("damage after self multiplier = &<$_finalDamage_>");
        return run $_finalDamage_.get();
    }
    function knockback() {
        player_motion.api.launch:$x = retina.__variable__:$output_vec3.X;
        player_motion.api.launch:$x *= EVAL (12000 / Retina.SCALE);
        player_motion.api.launch:$y = retina.__variable__:$output_vec3.Y;
        player_motion.api.launch:$y *= EVAL (12000 / Retina.SCALE);
        player_motion.api.launch:$z = retina.__variable__:$output_vec3.Z;
        player_motion.api.launch:$z *= EVAL (12000 / Retina.SCALE);
        function player_motion:api/launch_xyz;
    }
    function handle_rotation() {
        // Apply friction to angular velocity
        Hardcode.repeatList(
            "[AXIS]",
            () => {
                $sign_[AXIS] = 1;
                if (tf2.ang_vel_[AXIS]:@s < 0) {$sign_[AXIS] = -1;}
                tf2.ang_vel_[AXIS]:@s *= $sign_[AXIS];
                tf2.ang_vel_[AXIS]:@s *= GRENADE_ANGULAR_FRICTION;
                tf2.ang_vel_[AXIS]:@s /= 1000;
                if (tf2.collision:@s == 1) expand {
                    tf2.ang_vel_[AXIS]:@s *= GRENADE_ANGULAR_COLLISION_FRICTION;
                    tf2.ang_vel_[AXIS]:@s /= 1000;
                }
                tf2.ang_vel_[AXIS]:@s *= $sign_[AXIS];
            },
            ["x", "y", "z"]
        );

        // Apply angular acceleration based on surface grinding
        if (tf2.collision_x:@s != 0) {
            $surface_x = tf2.collision_x:@s; $surface_y = 0; $surface_z = 0;
            this.roll();
        }
        if (tf2.collision_y:@s != 0) {
            $surface_x = 0; $surface_y = tf2.collision_y:@s; $surface_z = 0;
            this.roll();
        }
        if (tf2.collision_z:@s != 0) {
            $surface_x = 0; $surface_y = 0; $surface_z = tf2.collision_z:@s;
            this.roll();
        }

        // Apply rotation
        Hardcode.repeatList(
            "[AXIS]",
            () => { $input_ang_vel_[AXIS] = tf2.ang_vel_[AXIS]:@s; },
            ["x", "y", "z"]
        );
        Math.angular_velocity_to_quaternion();
        Hardcode.repeatList(
            "[PARAM]",
            () => {
                $input_quaternion_1_[PARAM] = $output_quaternion_[PARAM];
                $input_quaternion_2_[PARAM] = tf2.quaternion_[PARAM]:@s;
            },
            ["r", "i", "j", "k"]
        );
        Math.multiply_quaternions();
        Hardcode.repeatList(
            "[PARAM]",
            () => { $input_quaternion_[PARAM] = $output_quaternion_[PARAM]; },
            ["r", "i", "j", "k"]
        );
        Math.normalize_quaternion();
        Hardcode.repeatList(
            "[PARAM]",
            () => { tf2.quaternion_[PARAM]:@s = $output_quaternion_[PARAM]; },
            ["r", "i", "j", "k"]
        );
        gren::rotation = [0.0f,0.0f,0.0f,1.0f];
        Hardcode.repeatLists(
            ["[PARAM]", "[INDEX]"],
            () => { gren::rotation[[INDEX]] = 0.001 * (float) tf2.quaternion_[PARAM]:@s; },
            [
                ["r", "i", "j", "k"],
                ["3", "0", "1", "2"]
            ]
        );
        @s::transformation.right_rotation = gren::rotation;
    }
    function roll() {
        Hardcode.repeatList(
            "[AXIS]",
            () => {
                $input_vector_1_[AXIS] = tf2.ang_vel_[AXIS]:@s;
                $input_vector_1_[AXIS] *= tf2.hitbox_[AXIS]:@s;
                $input_vector_1_[AXIS] /= 1000;
                $input_vector_2_[AXIS] = $surface_[AXIS];
            },
            ["x", "y", "z"]
        );
        Math.cross_product();
        Hardcode.repeatList(
            "[AXIS]",
            () => {
                if ($surface_[AXIS] == 0) {$output_vector_[AXIS] += tf2.mot_[AXIS]:@s;}
                $input_vector_1_[AXIS] = $output_vector_[AXIS];
                $input_vector_1_[AXIS] *= 1000;
                $input_vector_1_[AXIS] /= tf2.hitbox_[AXIS]:@s;

                $input_vector_1_[AXIS] *= 32;
            },
            ["x", "y", "z"]
        );
        Math.cross_product();
        Hardcode.repeatList(
            "[AXIS]",
            () => {
                tf2.ang_vel_[AXIS]:@s += $output_vector_[AXIS];
            },
            ["x", "y", "z"]
        );
    }

    function remove_tags() {
        tag @s remove tf2.batch;
        tag @s remove tf2.search;
        tag @s remove tf2.opponent;
        tag @s remove tf2.in_voxel;
        tag @s remove tf2.hit_by_projectile;
        tag @s remove tf2.closest_hit;
    }
    function cleanup() {
        execute on vehicle run this.cleanup();
        kill @s;
    }
    function end_of_life() {
        tag @s add tf2.delete;
        $explode_after_lifetime = @s::item.components.minecraft:custom_data.projectile.explode_after_lifetime;
        if (!$explode_after_lifetime) {
            return 0;
        }

        // else
        vars::radius = 0.01905 * (double) @s::item.components.minecraft:custom_data.projectile.explosion.radius;
        Projectile.explode() with vars::;
    }
}