new trim_pattern(medic) {
	"asset_id": "tf2:medic",
	"description": {
		"translate": "trim_pattern.tf2.medic"
	},
	"template_item": "minecraft:stick"
}
generate_body_model(medic);

JMC.python(`
make_weapon(
    Class.MEDIC, slot=0, name='Syringe Gun',
	display_name='Syringe Gun (WIP)',
    attributes={
		'damage': {'base': 10, 'maxRamp': 1.2}, 
		'attackDelay': 0.105, 
		'reloadDelay': [1.305, 1.305],
		'clip': 40, 'ammo': 160, 
		'interpolate_rmb': 1,
		'isProjectile': true
	},
	projectile={
		'spawn': [0.0, -0.45, 0.06], 
		'speed': 990, 'hitbox': 2,
		'deviation': {'min': 0, 'max': 200}, 
		'forces': {'gravity': 0.08},
		'block_collision': {'mode': Collision.STICK},
		'entity_collision': {'mode': Collision.IMPACT},
		'unreflectable': true
	},
	controls={'reload': 3},
	team_specific=True
)
`, jmc=true);

JMC.python(`
make_weapon(
    Class.MEDIC, slot=1, name='Medi Gun',
	display_name="Medi Gun (WIP)",
	team_specific=True,
	attributes={
		'clip': 1, 'damage': 0,
		'heal': {'base': 24, 'maxRamp': 3.0},
		'uber': {'build': 40.0, 'last': 8.0}
	},
	controls={'alt_fire': True, 'reload': 0},
	extra=meter(seconds=40.0, score='tf2.ubercharge', scale=1/4) 
)
`, jmc=true);

JMC.python(`
make_weapon(
    Class.MEDIC, slot=2, 
    name='Bonesaw',
    attributes={'damage': {'base': 65}, 'attackDelay': 0.8, 'range': 3.2, 'isMelee': true}
)
`, jmc=true);

class Class.Medic {

	function give() {
		clear @s;
		loot replace entity @s hotbar.0 loot tf2:class/medic/syringe_gun;
		loot replace entity @s hotbar.1 loot tf2:class/medic/medi_gun;
		loot replace entity @s hotbar.2 loot tf2:class/medic/bonesaw;
		loot replace entity @s armor.legs loot tf2:class/medic/leg_model;
		loot replace entity @s armor.chest loot tf2:class/medic/chest_model;

		lookup::inventory = @s::Inventory;
		tf2.primary_maxClip:@s = tf2.primary_clip:@s = lookup::inventory[0].components.minecraft:custom_data.attributes.clip;
		tf2.primary_maxAmmo:@s = tf2.primary_ammo:@s = lookup::inventory[0].components.minecraft:custom_data.attributes.ammo;
		tf2.secondary_maxClip:@s = tf2.secondary_clip:@s = lookup::inventory[1].components.minecraft:custom_data.attributes.clip;
		tf2.secondary_maxAmmo:@s = tf2.secondary_ammo:@s = lookup::inventory[1].components.minecraft:custom_data.attributes.ammo;

		if (entity @s[tag=!tf2.dead]) {
			return 1;
		}
		scoreboard players reset @s tf2.heal_target;
		tf2.ubercharge:@s = 0;
		tag @s remove tf2.uber_source;
	}
	@lazy
	function tick() {
		Weapons.get_ammo();
		HUD.actionbar();
		Weapons.set_ammo();

		this.inventory_lock();

		this.display_uber();

		$regen_modulo = 0008it54g_p_join:$__global__;
		$regen_modulo %= 20;
		if ($regen_modulo == 0) {
			this.natural_regen();
		}

		if (tf2.current_slot:@s != 1) {
			scoreboard players reset @s tf2.heal_target;
		}

		this.consume_uber();

		tag @s add self;
		if (tf2.current_slot:@s == 1 && !tf2.attack_delay:@s) {
			execute as @e[type=#tf2:player_like, tag=!self]
			        if score @s tf2.player.id = @p[tag=self] tf2.heal_target
			        at @s run this.apply_healing();
		}
		tag @s remove self;
	}
	function inventory_lock() {
		item replace entity @s weapon.offhand with air;
		
		loot replace entity @s hotbar.0 loot tf2:class/medic/syringe_gun;
		$_count_ = clear @s *[custom_data~{key: EVAL(Class.MEDIC * 10 + 0)}] 0;
		if ($_count_ != 1) expand {
			clear @s *[custom_data~{key: EVAL(Class.MEDIC * 10 + 0)}];
			loot replace entity @s hotbar.0 loot tf2:class/medic/syringe_gun;
		}

		loot replace entity @s hotbar.1 loot tf2:class/medic/medi_gun;
		$_count_ = clear @s *[custom_data~{key: EVAL(Class.MEDIC * 10 + 1)}] 0;
		if ($_count_ != 1) expand {
			clear @s *[custom_data~{key: EVAL(Class.MEDIC * 10 + 1)}];
			loot replace entity @s hotbar.1 loot tf2:class/medic/medi_gun;
		}

		loot replace entity @s hotbar.2 loot tf2:class/medic/bonesaw;
		$_count_ = clear @s *[custom_data~{key: EVAL(Class.MEDIC * 10 + 2)}] 0;
		if ($_count_ != 1) expand {
			clear @s *[custom_data~{key: EVAL(Class.MEDIC * 10 + 2)}];
			loot replace entity @s hotbar.2 loot tf2:class/medic/bonesaw;
		}

		loot replace entity @s armor.legs loot tf2:class/medic/leg_model;
		$_count_ = clear @s iron_leggings 0;
		if ($_count_ != 1) expand {
			clear @s iron_leggings;
			loot replace entity @s armor.legs loot tf2:class/medic/leg_model;
		}

		loot replace entity @s armor.chest loot tf2:class/medic/chest_model;
		$_count_ = clear @s iron_chestplate 0;
		if ($_count_ != 1) expand {
			clear @s iron_chestplate;
			loot replace entity @s armor.chest loot tf2:class/medic/chest_model;
		}

		loot replace entity @s armor.head loot tf2:head;
		$_count_ = clear @s paper 0;
		if ($_count_ != 1) expand {
			clear @s paper;
			loot replace entity @s armor.head loot tf2:head;
		}		
	}
	function natural_regen() {
		$natural_regen_amount = 3;
		if (tf2.ticks_since_hurt:@s >= 60) {
			$natural_regen_amount++;
		}
		if (tf2.ticks_since_hurt:@s >= 120) {
			$natural_regen_amount++;
		}
		if (tf2.ticks_since_hurt:@s >= 180) {
			$natural_regen_amount++;
		}

		tag @s add self;
		execute as @e[type=#tf2:player_like]
				if score @s tf2.player.id = @p[tag=self] tf2.heal_target
				if score @s tf2.health < @s tf2.max_health
				run $natural_regen_amount *= 2;
		tag @s remove self;

		tf2.health:@s += $natural_regen_amount;
		Player.clamp_health();
	}
	function select_heal_target() {
		tf2.team:$current = tf2.team:@s;
		tf2.batch_number:$current = tf2.batch_number:@s;

		tag @s add self;
		execute as @a[tag=!self,distance=.. MEDIGUN_CONNECT_RANGE]
		        if score @s tf2.batch_number = $current tf2.batch_number
		        if score @s tf2.team = $current tf2.team
		        run tag @s add tf2.valid_heal_target;
		execute as @a[tag=!self,distance=.. MEDIGUN_CONNECT_RANGE]
		        if score @s tf2.class matches Class.SPY
		        if score @s tf2.disguise.team = $current tf2.team
		        run tag @s add tf2.valid_heal_target;
		tag @s remove self;

		function retina:traverse/setup;

		scoreboard players reset @s tf2.heal_target;
		tf2.heal_target:@s = tf2.player.id:@e[type=#tf2:player_like, tag=tf2.valid_heal_target, scores={retina.hit=1..}];
		tf2.heal_target:@s ??= tf2.player.id:@e[type=#tf2:player_like, tag=tf2.valid_heal_target, sort=nearest, limit=1];
		
		tag @e remove tf2.valid_heal_target;
	}
	function disconnect() {
		tag @s remove tf2.uber_patient;
		scoreboard players reset @p[tag=self] tf2.heal_target;
	}
	function apply_healing() {
		if (entity @p[tag=self, distance= MEDIGUN_DISCONNECT_RANGE ..]) {
			return run this.disconnect();
		}
		$base_heal = @p[tag=self]::Inventory[{Slot: 1b}].components.minecraft:custom_data.attributes.heal.base;
		$maxRamp = @p[tag=self]::Inventory[{Slot: 1b}].components.minecraft:custom_data.attributes.heal.maxRamp * 50;
		$heal_amount = tf2.ticks_since_hurt:@s;
		$heal_amount -= 150;
		$heal_amount > 50;
		$heal_amount < $maxRamp;
		$heal_amount *= $base_heal;
		$heal_amount /= 50;

		$fraction_numerator = $heal_amount;
		$fraction_numerator %= 20;
		$heal_amount /= 20;

		$t = 0008it54g_p_join:$__global__;
		$t %= 20;
		$t *= $fraction_numerator;
		$t %= 20;
		if ($t < $fraction_numerator) {
			$heal_amount++;
		}
		tf2.health:@s += $heal_amount;
		if (tf2.health:@s > tf2.max_health:@s) {
			tag @s add tf2.overheal;
		}
		Player.clamp_health();

		$slow_threshold = tf2.max_health:@s;
		$slow_threshold *= 1425;
		$slow_threshold /= 1000;
		$uber_gain = 4;
		$id = tf2.player.id:@s;
		$medic_count = execute as @e[type=#tf2:player_like] if score @s tf2.heal_target = $id tf2.var;
		if (tf2.health:@s >= $slow_threshold) expand {
			$uber_gain /= 2;
			if ($medic_count >= 2) {
				$uber_gain /= 2;
			}
		}
		tf2.batch_number:$current = tf2.batch_number:@s;
		execute as @e[type=marker,tag=tf2.in_use,tag=tf2.setup_time]
				if score $current tf2.batch_number = @s tf2.batch_number
				run $uber_gain *= 3;
		if (entity @p[tag=self, tag=tf2.uber_source]) {
			tag @s add tf2.uber_patient;
		}

		execute at @p[tag=self] 
				anchored eyes
				facing entity @s eyes
				run this.particleFX();
	}
	function particleFX() {
		if (tf2.team:@p[tag=self] == 1) {
			particle minecraft:block{block_state:red_stained_glass} ~ ~-0.6 ~ 0 0 0 0 1 normal; 
		}
		if (tf2.team:@p[tag=self] == 2) {
			particle minecraft:block{block_state:blue_stained_glass} ~ ~-0.6 ~ 0 0 0 0 1 normal; 
		}

		execute positioned ^ ^ ^0.1
				unless entity @s[distance=..2]
				run this.particleFX();
	}


	function activate_uber() {
		$full_bar = @s::Inventory[{Slot: 1b}].components.minecraft:custom_data.attributes.uber.build * EVAL(4 * 20);
		if (tf2.ubercharge:@s >= $full_bar) {
			tag @s add tf2.uber_source;
		}
	}
	@lazy
	function display_uber() {
		execute positioned ~-3 ~-1 ~-3 
                unless entity @e[type=item_display,tag=tf2.control_point,dx=5,dy=1,dz=5]
                run Text.title(@s, "");
        Text.title(@s[tag=!tf2.can_cap], "");
		
		$full_bar = @s::Inventory[{Slot: 1b}].components.minecraft:custom_data.attributes.uber.build * EVAL(4 * 20);
		tf2.ubercharge:@s < $full_bar;
		$percent_ubercharge = tf2.ubercharge:@s;
		$percent_ubercharge *= 100;
		$percent_ubercharge /= $full_bar;
		Text.subtitle(@s, "&<$percent_ubercharge>% Ãœbercharge");
		title @s times 0 4 2;
	}
	function consume_uber() {
		$duration = @s::Inventory[{Slot: 1b}].components.minecraft:custom_data.attributes.uber.last * 100;
		$drain_rate = @s::Inventory[{Slot: 1b}].components.minecraft:custom_data.attributes.uber.build * 100;
		$drain_rate /= $duration;

		tf2.ubercharge:@s[tag=tf2.uber_source] -= $drain_rate;
		tag @s[scores={tf2.ubercharge=..0}] remove tf2.uber_source;
	}
}